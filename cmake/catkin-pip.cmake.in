message(STATUS "Loading catkin-pip.cmake from ${CMAKE_CURRENT_LIST_DIR}... ")

# TODO : we probably should move some of these variables into an env hook ?

if ( NOT CATKIN_PIP_REQUIREMENTS_PATH )
    # Needs to be here to avoid copying all dependent script into the workspace on configure step
    set (CATKIN_PIP_REQUIREMENTS_PATH ${CMAKE_CURRENT_LIST_DIR})
endif()

# note ${CMAKE_CURRENT_SOURCE_DIR}/env-hooks points to final package source env-hook DIR
# note ${CMAKE_CURRENT_LIST_DIR}/env-hooks points to catkin-pip env-hook DIR
catkin_add_env_hooks(42.site-packages SHELLS bash DIRECTORY ${CMAKE_CURRENT_LIST_DIR}/../env-hooks)

# Since we need (almost) the same configuration for both devel and install space, we create cmake files for each workspace setup.
set(DEVELSPACE True)
set(INSTALLSPACE False)
configure_file(${CMAKE_CURRENT_LIST_DIR}/catkin-pip-setup.cmake.in ${CMAKE_CURRENT_BINARY_DIR}/.catkin-pip-setup-devel.cmake @ONLY)

set(DEVELSPACE False)
set(INSTALLSPACE True)
configure_file(${CMAKE_CURRENT_LIST_DIR}/catkin-pip-setup.cmake.in ${CMAKE_CURRENT_BINARY_DIR}/.catkin-pip-setup-install.cmake @ONLY)

unset(DEVELSPACE)
unset(INSTALLSPACE)

# And here we need to do the devel workspace setup.
include(${CMAKE_CURRENT_BINARY_DIR}/.catkin-pip-setup-devel.cmake)


macro(catkin_pip_requirements requirements_txt )

    catkin_pip_requirements_prefix(${requirements_txt})

endmacro()



macro(catkin_pip_package)

    set (extra_macro_args ${ARGN})

    # Did we get any optional args?
    list(LENGTH extra_macro_args num_extra_args)
    if (${num_extra_args} GREATER 0)
        list(GET extra_macro_args 0 package_path)
        #message ("Got package_path: ${package_path}")
    else()
        set(package_path .)
    endif()

    catkin_pip_package_prefix(${package_path})

    # TODO this whole cmake is a actually a template, and we can probably integrate this in normal catkin flow
    # Setting up the command for install space for user convenience
    install(CODE "
        #Setting paths for install by including our configured install cmake file
        include(${CMAKE_CURRENT_BINARY_DIR}/.catkin-pip-setup-install.cmake)
        catkin_pip_package_prefix(${package_path})
    ")
endmacro()

# TODO :
# venv for easy pip install : investigate https://github.com/KitwareMedical/TubeTK/blob/master/CMake/TubeTKVirtualEnvSetup.cmake # maybe not a good idea. workspace do the job for ROS now.
# install pip packages to be embedded with install workspace. BUT not in catkin deb package...
# CPACK to deb : investigate https://cmake.org/pipermail/cmake/2011-February/042687.html
#
